apply plugin: 'com.android.application'
apply from: '../build-commons.gradle'
apply from: '../build-dependencies-common.gradle'

android {
    defaultConfig {
        project.ext.appName = 'android-playground'
        applicationId "pl.mobite.playground"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev"
            resValue "string", "app_name", "$project.ext.appName-dev"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", project.ext.appName
        }
    }

}

dependencies {
    implementation project(Modules.common)
    implementation project(Modules.navigation)
    implementation project(Modules.transitions)
}

task addVersionNameAndCodeAndTimestampToApkFileName() {
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.name != "debug") {
                def date = new Date().format("yyMMdd_HHmmss")
                def fileName = "${project.ext.appName}_${variant.name}_${variant.versionName}(${variant.versionCode})_${date}.apk"
                output.outputFileName = fileName
            }
        }
    }
}

gradle.taskGraph.whenReady {
    addVersionNameAndCodeAndTimestampToApkFileName
}